plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.9.10"
}

ext {
    degraphVersion = '0.1.4'
}

group 'de.obqo.gradle'
version "${degraphVersion}.1"

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

task createClasspathManifest {
    def outputDir = file("$buildDir/classpathManifest")
    def pluginClasspath = sourceSets.main.runtimeClasspath

    inputs.files pluginClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = pluginClasspath.join("\n")
    }
}

dependencies {
    compileOnly "de.schauderhaft.degraph:degraph-check:${degraphVersion}"

    // Work-around to prevent conflicting module version of groovy-all:
    // Make the implicit (and incorrect) dependency to groovy-all explicit so the dependency conflict resolution
    // will use this version of groovy (belonging to gradle 4.6) instead of an old one coming from an older gradle version.
    // See also https://github.com/gradle/gradle/issues/3698, https://github.com/gradle/gradle/issues/1370
    compile 'org.codehaus.groovy:groovy-all:2.4.12'

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'commons-io:commons-io:2.5'
    testRuntime files(createClasspathManifest)
}

def pluginId = 'de.obqo.gradle.degraph'
gradlePlugin {
    plugins {
        degraphPlugin {
            id = pluginId
            implementationClass = 'de.obqo.gradle.degraph.DegraphPlugin'
        }
    }
}

processResources {
    doLast {
        file("${buildDir}/resources/main/META-INF/gradle-plugins/${pluginId}.properties") << "default-tool-version=${degraphVersion}\n"
    }
}

pluginBundle {
    website = 'https://github.com/obecker/gradle-degraph'
    vcsUrl = 'https://github.com/obecker/gradle-degraph'

    plugins {
        degraphPlugin {
            id = 'de.obqo.gradle.degraph'
            displayName = 'Gradle Degraph Plugin'
            description = 'Gradle plugin that executes degraph dependency checks'
            tags = ['degraph', 'code-quality']
        }
    }
}
