plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.9.10"
}

ext {
    degraphVersion = '0.1.5-SNAPSHOT'
}

group 'de.obqo.gradle'
version "${degraphVersion}.1"

sourceCompatibility = 11

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Werror"
}

test {
    useJUnitPlatform()
}

task createClasspathManifest {
    def outputDir = file("$buildDir/classpathManifest")
    def pluginClasspath = sourceSets.main.runtimeClasspath

    inputs.files pluginClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = pluginClasspath.join("\n")
    }
}

dependencies {
    compileOnly "de.schauderhaft.degraph:degraph-check:${degraphVersion}"

    testImplementation gradleTestKit()
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testImplementation 'org.assertj:assertj-core:3.14.0'
    testImplementation 'commons-io:commons-io:2.6'
    testRuntime files(createClasspathManifest)
}

def pluginId = 'de.obqo.gradle.degraph'
gradlePlugin {
    plugins {
        degraphPlugin {
            id = pluginId
            implementationClass = 'de.obqo.gradle.degraph.DegraphPlugin'
        }
    }
}

processResources {
    doLast {
        file("${buildDir}/resources/main/META-INF/gradle-plugins/${pluginId}.properties") << "default-tool-version=${degraphVersion}\n"
    }
}

pluginBundle {
    website = 'https://github.com/obecker/gradle-degraph'
    vcsUrl = 'https://github.com/obecker/gradle-degraph'

    plugins {
        degraphPlugin {
            id = 'de.obqo.gradle.degraph'
            displayName = 'Gradle Degraph Plugin'
            description = 'Gradle plugin that executes degraph dependency checks'
            tags = ['degraph', 'code-quality']
        }
    }
}
